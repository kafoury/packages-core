# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

_extramodules=extramodules-3.7-MANJARO
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

pkgbase=('zfs')
pkgname=('linux37-zfs' 'zfs-common')
pkgver=0.6.1
pkgrel=1
makedepends=('linux37-headers' "spl-common=$pkgver")
provides=("zfs-modules=$pkgver")
arch=('i686' 'x86_64')
url='http://zfsonlinux.org/'
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/zfs/zfs-$pkgver.tar.gz)
groups=('manjarozfs' 'linux37-extramodules')
md5sums=('822cd73c139d89369d6c3944f8afe659')
license=('CDDL')

build() {
  cd ${srcdir}/zfs-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/sbin \
              --libdir=/usr/lib \
              --datadir=/usr/share \
              --includedir=/usr/include \
              --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-$pkgver \
              --with-config=kernel \
              --with-linux=/usr/src/linux-$_kernver \
              --with-linux-obj=/usr/src/linux-$_kernver
  make
}

package_linux37-zfs() {
  pkgdesc="Kernel modules for the Zettabyte File System."
  depends=(linux37-zfs=$pkgver zfs-utils=$pkgver 'linux37' 'mkinitcpio' "zfs-common=$pkgver")
  install=zfs.install
  cd ${srcdir}/zfs-$pkgver
  make DESTDIR=${pkgdir} install
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/zfs.install"
  sed -i -e "s/linux='.*'/linux=37/" "${startdir}/zfs.install"
  sed -i -e "s|${srcdir}/||g" "${pkgdir}/usr/src/zfs-$pkgver/${_kernver}/Module.symvers"
  mkdir -p "${pkgdir}/usr/lib/modules/${_extramodules}"
  for i in avl  nvpair  unicode  zcommon  zfs  zpios; do
     cp ${pkgdir}/lib/modules/${_kernver}/extra/${i}/*.ko "${pkgdir}/usr/lib/modules/${_extramodules}"
  done
  gzip ${pkgdir}/usr/lib/modules/${_extramodules}/*.ko
  rm -R "${pkgdir}/lib"
  mkdir -p "${srcdir}/_tmp/"
  mv "${pkgdir}/usr/src/zfs-${pkgver}/include" "${srcdir}/_tmp/include"
  mv "${pkgdir}/usr/src/zfs-${pkgver}/zfs_config.h.in" "${srcdir}/_tmp"
  mv "${pkgdir}/usr/src/zfs-${pkgver}/zfs.release.in" "${srcdir}/_tmp"
}

package_zfs-common() {
  pkgdesc='Kernel sources for the Zettabyte File System.'
  pkgrel=1
  mkdir -p "${pkgdir}/usr/src/zfs-${pkgver}/"
  mv "${srcdir}/_tmp/zfs_config.h.in" "${pkgdir}/usr/src/zfs-${pkgver}"
  mv "${srcdir}/_tmp/zfs.release.in" "${pkgdir}/usr/src/zfs-${pkgver}"
  mv "${srcdir}/_tmp/include" "${pkgdir}/usr/src/zfs-${pkgver}/include"
}

# Based on the file created for Arch Linux by:
# Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Bob Fanger < bfanger(at)gmail >
# Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgname=linux35-r8168
_pkgname=r8168
pkgver=8.035.00
pkgrel=2
pkgdesc="A kernel module for Realtek 8168 network cards"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux35')
makedepends=('linux35-headers')
provides=("$_pkgname=$pkgver")
groups=('linux35-extramodules')
source=(http://r8168.googlecode.com/files/$_pkgname-$pkgver.tar.bz2)
install=$_pkgname.install
sha256sums=('b904d31fc9bd6c97cc34479a0eba0e3a9bba5351a43b56dd5fff6b7dc1a5df73')

_extramodules=extramodules-3.5-MANJARO
_kernver="$(cat /usr/lib/modules/$_extramodules/version || true)"

build() {
	cd "$_pkgname-$pkgver"

	# avoid using the Makefile directly -- it doesn't understand
	# any kernel but the current.
	make -C /usr/lib/modules/$_kernver/build \
	SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
	EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
	modules
}

package() {
	cd "$srcdir/$_pkgname-$pkgver/src"
	install -D -m644 $_pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"

	# set the kernel we've built for inside the install script
	sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${_pkgname}.install"

	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

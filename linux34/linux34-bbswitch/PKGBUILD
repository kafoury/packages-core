#
# maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# maintainer: Roland Singer <roland[at]manjaro[dog]org>

pkgname=linux34-bbswitch
_pkgname=bbswitch
_extramodules=extramodules-3.4-MANJARO
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgver=0.6
pkgrel=1
pkgdesc="kernel module allowing to switch dedicated graphics card on Optimus laptops"
arch=('i686' 'x86_64')
url=("http://github.com/Bumblebee-Project/bbswitch")
license=('GPL')
depends=('linux34')
makedepends=('linux34-headers')
provides=("$_pkgname=$pkgver")
groups=('linux34-extramodules')
install=bbswitch.install
source=("https://github.com/Bumblebee-Project/bbswitch/archive/v${pkgver}.tar.gz")
sha256sums=('c4b156aeb180744d124b8fb6ff15b30642a1f0dd7be5cdca5fbe5f9add7b3cf8')

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  # KDIR is necessary even when cleaning
  make KDIR=/usr/src/linux-${_kernver} clean
  make KDIR=/usr/src/linux-${_kernver}
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  install -D -m644 bbswitch.ko $pkgdir/usr/lib/modules/${_extramodules}/bbswitch.ko
  # gzip -9 modules
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
  sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" $startdir/*.install
}

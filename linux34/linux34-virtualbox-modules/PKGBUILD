# Based on the file created for Arch Linux by:
# Ionut Biru <ibiru@archlinux.org>
# Sébastien Luttringer <seblu@aur.archlinux.org>

# Maintainer: Guinux <nuxgui@gmail.com>
# Maintainer: Philip Müller <philm@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>


pkgbase=linux34-virtualbox-modules
pkgname=('linux34-virtualbox-host-modules' 'linux34-virtualbox-guest-modules')
pkgver=4.2.6
pkgrel=2
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=("virtualbox-host-dkms>=$pkgver"
             "virtualbox-guest-dkms>=$pkgver"
             'dkms' 'expac' 'bc'
             'linux34' 'linux34-headers')

_kernver=$(expac -Q '%v' linux34-headers)-MANJARO
_extraver=extramodules-${_kernver%.*}-MANJARO


build() {
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf
  # build host modules
  msg2 'Host modules'
  dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
  # build guest modules
  msg2 'Guest modules'
  dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_linux34-virtualbox-host-modules(){
  pkgdesc='Host kernel modules for VirtualBox'
  depends=('linux34')
  replaces=('linux34-virtualbox-modules')
  conflicts=('linux34-virtualbox-modules')
  provides=('virtualbox-host-modules')
  install=virtualbox-host-modules.install
  groups=('linux34-extramodules')


  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -ie "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/virtualbox-host-modules.install"

  mkdir -p "$pkgdir/etc/modules-load.d"
  echo "vboxdrv" > "$pkgdir/etc/modules-load.d/linux34-virtualbox-host-modules.conf"
  echo "vboxnetadp" >> "$pkgdir/etc/modules-load.d/linux34-virtualbox-host-modules.conf"
  echo "vboxnetflt" >> "$pkgdir/etc/modules-load.d/linux34-virtualbox-host-modules.conf"
  echo "vboxpci" >> "$pkgdir/etc/modules-load.d/linux34-virtualbox-host-modules.conf"
}

package_linux34-virtualbox-guest-modules(){
  pkgdesc='Guest kernel modules for VirtualBox'
  license=('GPL')
  depends=('linux34')
  replaces=('linux34-virtualbox-manjaro-modules')
  conflicts=('linux34-virtualbox-manjaro-modules')
  provides=('virtualbox-guest-modules')
  install=virtualbox-guest-modules.install
  groups=('linux34-extramodules')


  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -ie "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/virtualbox-guest-modules.install"

  mkdir -p "$pkgdir/etc/modules-load.d"
  echo "vboxguest" > "$pkgdir/etc/modules-load.d/linux34-virtualbox-guest-modules.conf"
  echo "vboxsf" >> "$pkgdir/etc/modules-load.d/linux34-virtualbox-guest-modules.conf"
  echo "vboxvideo" >> "$pkgdir/etc/modules-load.d/linux34-virtualbox-guest-modules.conf"
}

# vim:set ts=2 sw=2 et:

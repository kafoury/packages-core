# Based on the file created for Arch Linux by:
# Ionut Biru <ibiru@archlinux.org>
# SÃ©bastien Luttringer <seblu@aur.archlinux.org>

# Maintainer: Guinux <nuxgui@gmail.com>
# Maintainer: Clittle <philm@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>


pkgbase=linux34-virtualbox-modules
pkgname=('linux34-virtualbox-host-modules' 'linux34-virtualbox-guest-modules')
pkgver=4.2.0
pkgrel=2
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux-headers'
             "virtualbox-host-source>=$pkgver"
             "virtualbox-guest-source>=$pkgver"
             'dkms'
             'linux34' 'linux34-headers')

_extramodules=extramodules-3.4-MANJARO
_kernver="$(cat /usr/lib/modules/${_extramodules}/version || true)"


build() {
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf
  # build host modules
  msg2 'Host modules'
  dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
  # build guest modules
  msg2 'Guest modules'
  dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_linux34-virtualbox-host-modules(){
  pkgdesc='Host kernel modules for VirtualBox'
  depends=('linux34')
  replaces=('linux34-virtualbox-modules')
  conflicts=('linux34-virtualbox-modules')
  install=virtualbox-host-modules.install
  groups=('linux34-extramodules')


  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules"
  cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -ie "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "$startdir/virtualbox-host-modules.install"

  mkdir -p "$pkgdir/etc/modules-load.d"
  echo "vboxdrv" > "$pkgdir/etc/modules-load.d/linux34-virtualbox-host-modules.conf"
  echo "vboxnetadp" >> "$pkgdir/etc/modules-load.d/linux34-virtualbox-host-modules.conf"
  echo "vboxnetflt" >> "$pkgdir/etc/modules-load.d/linux34-virtualbox-host-modules.conf"
  echo "vboxpci" >> "$pkgdir/etc/modules-load.d/linux34-virtualbox-host-modules.conf"
}

package_linux34-virtualbox-guest-modules(){
  pkgdesc='Guest kernel modules for VirtualBox'
  license=('GPL')
  depends=('linux34')
  replaces=('linux34-virtualbox-manjaro-modules')
  conflicts=('linux34-virtualbox-manjaro-modules')
  install=virtualbox-guest-modules.install
  groups=('linux34-extramodules')


  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules"
  cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -ie "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "$startdir/virtualbox-host-modules.install"

  mkdir -p "$pkgdir/etc/modules-load.d"
  echo "vboxguest" > "$pkgdir/etc/modules-load.d/linux34-virtualbox-guest-modules.conf"
  echo "vboxsf" >> "$pkgdir/etc/modules-load.d/linux34-virtualbox-guest-modules.conf"
  echo "vboxvideo" >> "$pkgdir/etc/modules-load.d/linux34-virtualbox-guest-modules.conf"
}

# vim:set ts=2 sw=2 et:

# Based on the file created for Arch Linux by:
# Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Based on SUSE spec https://build.opensuse.org/package/files?package=rt3562sta&project=driver%3Awireless

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgbase=rt3562sta
pkgname=('linux38-rt3562sta' 'rt3562sta-common')
pkgver=2.4.1.1
pkgrel=1
_subpkgrel=2
arch=(i686 x86_64)
url="http://www.mediatek.com/en/Products/support.php?sn=501"
license=('GPL')
makedepends=('linux38' 'linux38-headers')
source=(
	http://dl.dropbox.com/u/362439/DPO_RT3562_3592_3062_LinuxSTA_V${pkgver}_20101217.tgz
	$pkgbase-$pkgver-config.patch
	$pkgbase-$pkgver-gcc-warnings-x86_64.patch
	$pkgbase-$pkgver-WPA-mixed.patch
	$pkgbase-$pkgver-convert-devicename-to-wlanX.patch
	$pkgbase-$pkgver-remove-potential-conflicts-with-rt2860sta.patch
	$pkgbase-$pkgver-return_nonvoid.patch
	$pkgbase-$pkgver-reduce_debug_output.patch
	$pkgbase-$pkgver-remove_date_time.patch
)

_extramodules=extramodules-3.8-MANJARO
_kernver="$(cat /usr/lib/modules/$_extramodules/version || true)"

build() {
	cd "$srcdir/DPO_RT3562_3592_3062_LinuxSTA_V2.4.1.1_20101217"
	patch -p0 -i "$srcdir/$pkgbase-$pkgver-config.patch"
	[ "$CARCH" == "x86_64" ] && patch -p0 -i "$srcdir/$pkgbase-$pkgver-gcc-warnings-x86_64.patch"
	patch -p0 -i "$srcdir/$pkgbase-$pkgver-WPA-mixed.patch"
	patch -p0 -i "$srcdir/$pkgbase-$pkgver-convert-devicename-to-wlanX.patch"
	patch -p0 -i "$srcdir/$pkgbase-$pkgver-remove-potential-conflicts-with-rt2860sta.patch"
	patch -p0 -i "$srcdir/$pkgbase-$pkgver-return_nonvoid.patch"
	patch -p0 -i "$srcdir/$pkgbase-$pkgver-reduce_debug_output.patch"
	patch -p0 -i "$srcdir/$pkgbase-$pkgver-remove_date_time.patch"

	# clean up this mess of mixing RT2860STA with RT3562STA
	# in documentation files
	mv RT2860STA.dat RT3562STA.dat
	mv RT2860STACard.dat RT3562STACard.dat
	sed -i 's/2860/3562/g' *STA* iwpriv_usage.txt

	# as we change the default name of the interface from raX to wlanX, change respective references in documentation, too
	sed -i 's|ra0|wlan0|g' *.txt README* *.dat
	sed -i 's|ra1|wlan1|g' *.txt README* *.dat
	sed -i 's|ra2|wlan2|g' *.txt README* *.dat

	export EXTRA_CFLAGS="-DVERSION=$pkgver"

	# this Makefile is far too strict...
	echo "LINUX_SRC = /usr/lib/modules/$_kernver/build" >> Makefile

	make
}

package_rt3562sta-common() {
	pkgdesc="Ralink RT3562 PCI WLAN adaptors common files"
        arch=(any)
        pkgrel="$_subpkgrel"
	cd "$srcdir/DPO_RT3562_3592_3062_LinuxSTA_V2.4.1.1_20101217"

	install -Dm 0640 RT3562STA.dat "$pkgdir/etc/Wireless/RT3562STA/RT3562STA.dat"
	install -dm 0755 "$pkgdir/usr/share/doc/$pkgbase"
	install -m 0644 iwpriv_usage.txt README* RT3562STA* sta_ate_iwpriv_usage.txt "$pkgdir/usr/share/doc/$pkgbase"
}

package_linux38-rt3562sta() {
	provides=("$_pkgname=$pkgver")
	groups=('linux38-extramodules')
	pkgdesc="Ralink RT3562 PCI WLAN adaptors kernel module"
	depends=('linux38' 'rt3562sta-common')
	groups=('linux38-extramodules')
        install=$pkgbase.install
	cd "$srcdir/DPO_RT3562_3592_3062_LinuxSTA_V2.4.1.1_20101217"

	install -Dm 0644 os/linux/$pkgbase.ko "$pkgdir/usr/lib/modules/$_extramodules/$pkgbase.ko"

	# set the kernel we've built for inside the install script
	sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${pkgbase}.install"

	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

md5sums=('a5e9af70abccdb7b86d1ef605bb34491'
         '5cff38fb070fd181cf4beb52acc99616'
         'ca40e951c0799d395a74bf274459b950'
         '743b45ad2bf60bf757e53439c26edb71'
         'f2d0fbe845415cdb4f31e365876f740e'
         '874619383576e38c5c048f0a068c9844'
         '466df4e09896836082db417e52f0224e'
         'edc79dd0de1e8fabcec1e4791feb4391'
         'c8e174ea362bf94ce46038f8388439b6')

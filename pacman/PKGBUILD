# Based on the file created for Arch Linux by:
# Dan McGee <dan@archlinux.org>
# Dave Reisner <dreisner@archlinux.org>

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Guinux <guillaume@manjaro.org>

pkgname=pacman
pkgver=4.1.2
pkgrel=4
pkgdesc="A library-based package manager with dependency support"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
depends=('bash>=4.2.042-2' 'glibc>=2.17-2' 'libarchive>=3.1.2' 'curl>=7.19.4'
         'gpgme' 'pacman-mirrorlist>=20130418' 'archlinux-keyring' 'manjaro-keyring')
checkdepends=('python2' 'fakechroot')
makedepends=('asciidoc')
optdepends=('fakeroot: for makepkg usage as normal user'
            'haveged: for pacman-init.service')
provides=('pacman-contrib' 'pacman-init')
conflicts=('pacman-contrib' 'pacman-init')
replaces=('pacman-contrib' 'pacman-init')
backup=(etc/pacman.conf etc/makepkg.conf)
install=pacman.install
options=(!libtool)
source=(ftp://ftp.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz{,.sig}
        pacman.conf.i686
        pacman.conf.x86_64
        makepkg.conf
        pacman-updated-locales.patch
        pacman-remove-sync-first-option.patch
        etc-pacman.d-gnupg.mount
        pacman-init.service)


prepare() {
  cd $srcdir/$pkgname-$pkgver

  patch -Rp1 -i $srcdir/pacman-remove-sync-first-option.patch
  patch -p1 -i $srcdir/pacman-updated-locales.patch

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc \
    --with-scriptlet-shell=/usr/bin/bash \
    --with-ldconfig=/usr/bin/ldconfig
  rm $srcdir/$pkgname-$pkgver/lib/libalpm/po/*gmo
  cd $srcdir/$pkgname-$pkgver/lib/libalpm/po/
  make update-gmo
  rm $srcdir/$pkgname-$pkgver/scripts/po/*gmo
  cd $srcdir/$pkgname-$pkgver/scripts/po/
  make update-gmo
  rm $srcdir/$pkgname-$pkgver/src/pacman/po/*gmo
  cd $srcdir/$pkgname-$pkgver/src/pacman/po/
  make update-gmo
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make
  make -C contrib
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  make DESTDIR=$pkgdir -C contrib install

  # install Arch specific stuff
  install -dm755 $pkgdir/etc
  install -m644 $srcdir/pacman.conf.$CARCH $pkgdir/etc/pacman.conf
  
  case "$CARCH" in
    i686)    
      mycarch="i686"
      mychost="i686-pc-linux-gnu"
      myflags="-march=i686"
      ;;
    x86_64)
      mycarch="x86_64"
      mychost="x86_64-unknown-linux-gnu"
      myflags="-march=x86-64"
      ;;
  esac
  install -m644 $srcdir/makepkg.conf $pkgdir/etc/
  # set things correctly in the default conf file
  sed -i $pkgdir/etc/makepkg.conf \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"
    
  # put bash_completion in the right location
  install -dm755 ${pkgdir}/usr/share/bash-completion/completions
  mv ${pkgdir}/etc/bash_completion.d/pacman \
    ${pkgdir}/usr/share/bash-completion/completions
  rmdir ${pkgdir}/etc/bash_completion.d

  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
  done

  # replace rankmirrors
  rm "$pkgdir/usr/bin/rankmirrors"
  ln -sfv "/usr/bin/pacman-mirrors" "$pkgdir/usr/bin/rankmirrors"

  # install pacman-init
  install -dm755 $pkgdir/usr/lib/systemd/system/
  install -m644 $srcdir/etc-pacman.d-gnupg.mount $pkgdir/usr/lib/systemd/system/etc-pacman.d-gnupg.mount
  install -m644 $srcdir/pacman-init.service $pkgdir/usr/lib/systemd/system/pacman-init.service
}
sha256sums=('7c059b444735ee254cfbd8dc2fac8c5d61453601f8adaf001c7038cdc673334c'
            'SKIP'
            '14e13173498bce6b8837f5d635b819ffc8412c164ccac36d6d62207685aed83d'
            '75e55f2ba3aaf76e839c6538df41219f047af7eddc7903ad8c25754db6b2b100'
            '0968ffa405f3f2ee8ae30bfb1366602f2f4478e0f44ca8bfa1fcc4119121920f'
            '9707a153a77d0d36c99b930ac619aeeb862336c5765de20cd0a8199c8af8533e'
            '4ae031c1110e9e4b05a305856f623b7d15b7915264024a5e7c5f8fcee2032f14'
            'b6d14727ec465bb66d0a0358163b1bbfafcb4eaed55a0f57c30aabafae7eed68'
            'ca4aa967ccfb9affab23fd61ad6d16f330b2810e5f9de93deec49ea7872369ae')
